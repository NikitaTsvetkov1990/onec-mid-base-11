
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// НачалоДоработкиКонтактныеЛица
	// Цветков Н.А. Задача №1 "Внесение изменений в конфигурацию на поддержке" от 14.10.2024
	// Новый код
	ЭлементКонтактноеЛицо = Элементы.Вставить("КЛ_КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.КЛ_КонтактноеЛицо";
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	
	ЭлементКонтактноеЛицо = Элементы.Вставить("КЛ_СогласованнаяСкидка", Тип("ПолеФормы"), Элементы.ГруппаСуммаДокумента, Элементы.ЯчейкаСуммаДокумента);
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.КЛ_СогласованнаяСкидка";
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.УстановитьДействие("ПриИзменении", "КЛ_СогласованнаяСкидкаПриИзменении");
	
	Команда = Команды.Добавить("ПересчетСУчетомСкидки");
	Команда.Заголовок = "Пересчет с учетом скидки";
	Команда.Действие = "КЛ_ПересчетСУчетомСкидки";
	
	ЭлементКнопка = Элементы.Вставить("КнопкаПересчетСУчетомСкидки", Тип("КнопкаФормы"), Элементы.ГруппаСуммаДокумента, Элементы.КЛ_СогласованнаяСкидка);
	ЭлементКнопка.ИмяКоманды = "ПересчетСУчетомСкидки";
	ЭлементКнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	// КонецДоработкиКонтактныеЛица
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура КЛ_ПересчетСУчетомСкидки(Команда)
	
	// НачалоДоработкиКонтактныеЛица
	// Цветков Н.А. Задача №1 "Внесение изменений в конфигурацию на поддержке" от 14.10.2024
	// Новый код
	КЛ_ПересчитатьСуммуТоваров();
	КЛ_ПересчитатьСуммуУслуг();
	// КонецДоработкиКонтактныеЛица
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	
	// НачалоДоработкиКонтактныеЛица
	// Цветков Н.А. Задача №1 "Внесение изменений в конфигурацию на поддержке" от 14.10.2024
	// Новый код
	Скидка = Окр(ТекущиеДанные.Цена * ТекущиеДанные.Количество * Объект.КЛ_СогласованнаяСкидка / 100, 2);
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество - Скидка;
	// КонецДоработкиКонтактныеЛица
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура КЛ_ПересчитатьСуммуТоваров()
	
	// НачалоДоработкиКонтактныеЛица
	// Цветков Н.А. Задача №1 "Внесение изменений в конфигурацию на поддержке" от 14.10.2024
	// Новый код
	Для Каждого ТекущиеДанные Из Объект.Товары Цикл
		
		РассчитатьСуммуСтроки(ТекущиеДанные);
		
	КонецЦикла;
	
	РассчитатьСуммуДокумента();
	// КонецДоработкиКонтактныеЛица
	
КонецПроцедуры

&НаКлиенте
Процедура КЛ_ПересчитатьСуммуУслуг()
	// НачалоДоработкиКонтактныеЛица
	// Цветков Н.А. Задача №1 "Внесение изменений в конфигурацию на поддержке" от 14.10.2024
	// Новый код
	Для Каждого ТекущиеДанные Из Объект.Услуги Цикл
		
		РассчитатьСуммуСтроки(ТекущиеДанные);
		
	КонецЦикла;
	
	РассчитатьСуммуДокумента();
	// КонецДоработкиКонтактныеЛица
КонецПроцедуры

&НаКлиенте
Асинх Процедура КЛ_СогласованнаяСкидкаПриИзменении(Элемент)
	// НачалоДоработкиКонтактныеЛица
	// Цветков Н.А. Задача №1 "Внесение изменений в конфигурацию на поддержке" от 14.10.2024
	// Новый код
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	Вопрос = ВопросАсинх("Вы хотите применить скидку?", РежимДиалогаВопрос.ДаНет);
	
	Ответ = Ждать Вопрос;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	КЛ_ПересчитатьСуммуТоваров();
	КЛ_ПересчитатьСуммуУслуг();
	// КонецДоработкиКонтактныеЛица
КонецПроцедуры


#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
